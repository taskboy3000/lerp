#!/usr/bin/env perl

use warnings;
use strict;

use FindBin;
use lib
    "$FindBin::Bin/../lib",
    ;

use Cwd;
use Getopt::Long;
use Path::Class::Dir;
use Plerd::Init;
use Plerd::Util;
use Plerd;
use Text::Wrap qw(wrap);

our $gDEFAULT_INIT_PATH = 'plerd';

main();
exit(0);

#--------------
# Subs
#--------------
sub do_initialize {
    my ($init_path) = @_;

    my $is_using_default = 0;
    unless (length $init_path) {
        $init_path        = Path::Class::Dir->new(cwd, $gDEFAULT_INIT_PATH,)->stringify;
        $is_using_default = 1;
    }

    my $messages = Plerd::Init::initialize($init_path, $is_using_default);
    print wrap(q{}, q{}, @$messages);
    print "\n";
} # end sub do_initialize


sub get_plerd_config_from_file {
    my ($config_file) = @_;
    if (!-e $config_file) {
        die("'$config_file' not found.  Run $0 init.\n");
    }

    my $cfg = Plerd::Util::read_config_file($config_file);
    for my $property ('base_uri', 'image') {
        if (!ref $cfg->{$property}) {
            $cfg->{$property} = URI->new($cfg->{$property});
        }
    }
    return $cfg;
} # end sub get_plerd_config_from_file


sub do_publish_all {
    my ($config_file, $verbose) = @_;

    my $cfg   = get_plerd_config_from_file($config_file);
    my $plerd = Plerd->new($cfg);

    $plerd->publish_all(verbose => $verbose);
} # end sub do_publish_all


sub do_help {
    print <<"EOT";
$0 - manage plerd

USAGE:
  $0 [COMMAND] [OPTIONS]

Commands:
  --init    Create a new plerd instance
  --help    This screen
  --publish [default] Publish new posts
  --publish-all Publish all posts

Options:
  --verbose Enable verbose output

EOT
} # end sub do_help

sub main {
    my $verbose = 0;
    my $config_file;
    my $init_path;
    my $help;
    my $publish;
    my $publish_all;

    GetOptions(
        'config=s' => \$config_file,
        'init:s'   => \$init_path,
        'verbose'  => \$verbose,
        'help'     => \$help,
        'publish'  => \$publish,
        'publish-all' => \$publish_all,
    );

    if (!defined $config_file && -e "$ENV{HOME}/.plerd.conf") {
        $config_file = "$ENV{HOME}/.plerd.conf";
    }

    if (defined $init_path) {
        do_initialize($init_path, $config_file, $verbose);
    } elsif ($publish) {
        do_publish_all($config_file, $verbose);
    } else {
        do_help();
    }
} # end sub main

